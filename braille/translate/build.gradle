apply plugin: 'com.android.library'
apply from: "../../shared.gradle"

android {
    externalNativeBuild {
        ndkBuild {
            path file('src/phone/jni/Android.mk')
        }
    }
    // liblouiswrap.so is linked with c++_shared; pack libc++_shared.so so it's in the AAR/APK.
    sourceSets {
        main {
            def ndkDir = android.ndkDirectory
            if (ndkDir != null && ndkDir.exists()) {
                def cxxLibs = new File(ndkDir, "sources/cxx-stl/llvm-libc++/libs")
                if (cxxLibs.exists()) {
                    jniLibs.srcDirs += cxxLibs
                }
            }
        }
    }
}

dependencies {
    implementation project(':brailleinterfaces')
    implementation project(':proguard')
    implementation project(':utils')
}

task createTranslationTablesZip(type: Zip){
    archiveName="translationtables.zip"
    destinationDir = file('src/phone/res/raw/')
    from fileTree('./src/phone/tables/')
    into('liblouis/tables')
}

preBuild.dependsOn createTranslationTablesZip
